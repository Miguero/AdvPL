/*
    Esse exemplo faz parte da série do YouTube, Maratona de Exemplos, do canal Terminal de Informação, 
    caso queira ver esse exemplo rodando em vídeo, acesse o seguinte link:     https://terminaldeinformacao.com/2024/06/29/exibindo-textos-atraves-da-tsay-maratona-advpl-e-tl-507/
*/


//Bibliotecas
#Include "tlpp-core.th"

//Declaração da namespace
Namespace custom.terminal.youtube

/*/{Protheus.doc} User Function tryCatch
Aciona tratativa e erro nos códigos
@type Function
@author Atilio
@since 12/03/2024
@see https://tdn.totvs.com/display/tec/Try...Catch
@example custom.terminal.youtube.u_tryCatch()
@obs Exemplo 506 da série Maratona de Exemplos, foi padronizado conforme link: https://tdn.totvs.com/pages/releaseview.action?pageId=633537898
/*/

User Function tryCatch()
    Local aArea    := FWGetArea() As Array
    Private oError := Nil         As Object

    //Tenta executar os comandos
    TRY

        //Se a pergunta for confirmada, irá forçar passar pelo ErrorClass
        If FWAlertYesNo("Deseja forçar o erro e a saída do Try?", "Continua?")
            forceError()
        EndIf
        
        //Mostra mensagem
        FWAlertSuccess("Sucesso na execução do Try", "Teste Try ... Catch")

    //Caso houve falha, captura e exibe
    CATCH oError
        FWAlertError("Erro gerado: " + oError:Description , "Teste Try ... Catch")
    ENDTRY
  
    FWRestArea(aArea)
Return

Static Function forceError()
    oError := ErrorClass():New()
    oError:genCode := 19584
    oError:description := "Erro forçado dentro da função forceError()"
    THROW oError
Return